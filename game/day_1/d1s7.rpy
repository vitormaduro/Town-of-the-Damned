# Discussing the evidence
label d1s7:
    scene bg rons_office with fade

    show marley neutral at right with dissolve
    show danny neutral at center with dissolve
    show ron moody at left with dissolve

    r "Danny, you could have warned me that the doggone sheriff was Terry frickin' Maddux."
    b "Sorry, sorry. But Terry ain't that bad."
    m "You really hate discount Biff Tannen, huh Ron?"
    b "Who?"
    m "Uh... bad guy from {i}Back to the Future{/i}?"
    r "Terry was our high school bully. He beat me up. Stole my lunch money. Beat me up. Made me give him my stuff. Beat me up. Threw me in the dumpster. Beat me up. Ripped up my homework. Beat me up. Poured food all over me. And did I mention he beat me up?"
    b "You're exaggerating."
    r "I once had to get three stitches 'cos he slammed my head into a desk! Why are you defending him? He was even worse to you."
    b "It was just childhood teasing; I got over it. And hey, you beat him up pretty bad yourself that one time."

    show ron confused with dissolve

    r "When?"
    b "The- the day that you... left."

    show ron neutral with dissolve

    r "Oh right, yeah. I remember now. Well, he deserved it."

    show danny annoyed with dissolve

    b "Well, everything kind of blew up for poor Terry after you left."

    show ron confused with dissolve

    r "What do you mean?"
    b "You don't know?"
    r "Know what?"
    b "Well... do you remember how Terry got a full ride to Ohio State on a football scholarship?"
    r "Yeah. He was the star quarterback in high school. He always talked about how excited he was for it."
    b "Yeah, it was his dream since he was a kid. Well, after you left, someone sent Ohio State an old video of him being... crappy to you. And they put it online—it went vial. No one knows who it was, but the university rescinded the scholarship."
    r "Oh. Oh wow. I hate to feel sorry for the guy, but... jeez."
    m "Let's put aside that muscled twit for now. What have you learnt, Ron? Give me the rundown."

    show danny neutral with dissolve
    show ron neutral with dissolve

    r "Okay, let's talk about the library first. Occult section looks like it's been put through a shredder."
    m "Alright, why?"
    r "I'd guess either to stop people realizing a book or two's gone missing, or to make it harder to research what's going on. Maybe both."
    m "Makes sense."
    r "Scene two: mayor's office. Ghouls ran amok and smashed the mayor's secretary's computer. She then fired him."
    m "Angry, screaming weirdo in the car park?"
    r "Angry, screaming weirdo in the car park. Strange timing. Especially since the mayor seems to loathe him."
    m "Suspicious."
    r "Very."
    b "What, Norman? Nah, he got the job as a favor—his pa used to be on the city council before he died. Norm was awful at his work; he was a jackass to everyone, careless, lazy, whatever. But my mom played nice to butter up his old man. I think this whole ghoul thing's made her a bit more daring, so she finally gave him the can."
    r "Well, we don't know what order the places were attacked, but your mom said her place was attacked on the Friday night. That's the same day that she sent the letter to fire Norman. Gives him a motive, at least for that attack."
    m "But why attack his own desk? Or any desk, why not attack her? No, my theory is that there was something important on the mayor's computer. But ghouls aren't exactly the brightest of magical sidekicks. One computer looks like any other to them."
    r "Hmm, interesting thought. Anyway, scene three: the sheriff's office. Again—very targeted attack"
    m "The chief deputy."
    r "Uh-huh. Second computer trashed."
    m "Summoner's a technophobe?"
    r "But only for {i}certain{/i} people's tech? And I found this—"

    "I pulled the stick from my bag."

    m "A USB stick?"
    r "Found it hidden in the ruins of the desk."
    m "Well, that's got to be important."

    "I notice something else in the bag."

    show ron surprised with dissolve

    r "Oh shoot—there's the book. I forgot about that; I didn't mean to bring it."
    m "What is it?"
    r "Old yearbook from the local high school. Found it in the desk. I don't think it's important, but we'll stick it in the evidence box."
    b "Evidence box?"
    r "That big metal lockbox under the desk—we put all the evidence in there to keep it safe."
    b "Ah right, I see. Well, I guess all that's left now is to go interrogate Seb then lock him up!"
    m "Kind of jumping the gun a bit there, bucko."
    b "Why? We've found his stuff at all the crime scenes. What more do we need?"
    m "A motive, maybe?"
    b "Revenge? Rage?"
    r "What do you mean?"

    show danny serious with dissolve

    b "I wouldn't be surprised if Sebastian Daley wanted to see this whole town burned to the ground."

    show ron confused with dissolve

    r "Why?"
    b "He's not a popular guy. That's all you need to know."
    r "What the hell are you going on about?"

    # TODO: SFX- Ring ring

    "The sound of the office phone makes me jump. I pick it up as Danny fiddles awkwardly with a pen."

    show ron neutral with dissolve

    r "Hello?"
    t "Ron, my man!"
    r "Oh. Hey Terry."
    t "So me and my boys are going patrol the town tonight in case the ghouls show their moldy-rotten faces. Figured I'd invite you, Danny, and that cute little assistant of yours to tag along. Meet us at eight, outside the sheriff's office?"
    r "Oh, right. Thanks. We'll be there."
    t "Awesome! You'll get to see how {i}real{/i} detectives act."

    show ron moody with dissolve

    "I roll my eyes."

    r "We'll head to the station soon. See you then."

    # TODO: SFX- phone being put down

    r "Well, it sounds like we're in for a delightful evening. Terry has invited us to patrol the town with him."

    show marley moody with dissolve

    m "And you couldn't have told him we had better things to do? Like... anything that didn't involve him?"

    show danny thoughtful with dissolve

    b "Maybe this will be a good opportunity for you, Ron. To, you know, bond with Terry. For you both to put the past behind you."
    r "I'd rather bond with a benign tumor."

    # TODO: SFX- a newspaper drops into a mailbox.

    show danny moody with dissolve

    b "Aww crap."

    show ron curious with dissolve

    r "What?"
    b "The paper. The Kingston Rag."

    show marley confused with dissolve

    m "Why does that make you mad?"
    b "There's this gossip columnist, Cléber Chalá, who's always published in it. Well, I mean, that's probably a pen name. No one in the town has that name."

    show marley thoughtful with dissolve

    m "I've heard that name before. I think he's a football player. Or I guess soccer for you guys."
    b "Well whoever {i}our{/i} Cléber is, they're a pain in my ma's ass. Basically, they can write whatever they want scot-free as long as they say 'there's a rumor that...' in front of it."
    m "Why read it if it annoys you?"
    b "Well, the problem, professor, is that the whole darned town reads this stupid column. These idiots eat this garbage up. I need to know what it says so I can be prepared."

    show marley moody with dissolve

    m "Alright, alright, no need for the attitude. Let's see this dreaded column then."

    call screen d1s7_newspaper

screen d1s7_newspaper():
    image "black"

    side "c r":
        area (100, 100, 1600, 700)

        viewport id "vp":
            draggable True

            text "Chaos Grips Kingston!\n\nI am sure that, just like me, many of you, my darling devoted readers, are deeply disturbed by recent happenings. You may be aware that evidence has emerged showing certain unknown members of our community behaving in a manner that is frankly unacceptable.\nAlthough our authorities have been doing their utmost to keep these events quiet, it's come to the attention of this here writer that the office of our mayor, the sheriff's office, and our community library have all been brutally attacked. I can only assume this is the work of local youths looking to cause a ruckus.\nIn my humble opinion, this disrespectful string of attacks can only be blamed on the moral decay of today's youngins. I don't know if they're restless on account of the controversial changes our mayor has inflicted on Kingston High by diverting funds away from the football team, where our youngsters can burn off excess energy, and towards things like the famously dull chess club, a useless creative writing group, and an unnecessary LBGT society. Or perhaps we should blame this delinquency on the questionable appointment of a half-elf to the teaching staff.  Either way, if our idealistic mayor and ineffective town council don't tackle this issue at its root, I'd bet my bottom dollar that we won't see an end to these attacks.\nIf these foolish shenanigans were the only problems our community faced, that would be bad enough. But as the proud owner of the 'Town Gossip' title, part of my duties is to be the breaker of bad news. And as such, I need to break something to you good people:\nMy sources have informed me that we have a thief hiding in the high echelons of our town. Word has it that money has gone missing from the town's treasury.\nThis isn't confirmed and is just gossip, but apparently, every month or so, a sum of around $5,000 has gone missing! That isn't an amount to sniff at! Although it's not that much compared to the council's budget, it still means that someone who has authority over the town is deprived enough to steal our money! Like I said, I haven't had this confirmed, but your faithful Cléber is gonna be like a dog with a bone as far as this is concerned! I will hold the thief or thieves accountable, and I will make sure that you, good people of Kingston, know exactly what is going on in our town! I will shine a light in every dark, deprived crack of our home!"

        vbar value YScrollValue("vp")

    imagebutton idle "images/ui/next_button.png" action Jump("d1s7_after_newspaper"):
        xalign 0.5
        yalign 0.9

label d1s7_after_newspaper:
    show marley confused with dissolve
    show ron shocked with dissolve

    r "Jeez—whoever this writer is, they really hate your ma'!"
    b "They hate everyone. Attack anyone they can. Anyone in the public eye. Like I've said, we're trying to keep the whole 'ghoul' thing under the rug. But if whoever this Cléber guy is finds out, half of the local government would be ripped apart."

    show ron thoughtful with dissolve

    r "I see."

    show marley thoughtful with dissolve

    m "We'll have to keep an eye out for this writer."
    b "Oh definitely. Especially because you two will probably end up in the column as soon as Cléber finds out about you. They adore gossiping about strangers. And you two are mighty strange."
    m "True."
    r "Well, nothing we can do about it for now. We need to focus on the case."

    show ron thoughtful with dissolve
    show danny neutral with dissolve

    r "But here's what's odd: our question was 'what order did the ghoul attacks happen in?' But of the three people we've interviewed, they've all said a different order!"

    show marley surprised with dissolve

    m "Really?"
    r "Yeah. We know the attacks happened Thursday, Friday, and Saturday."
    r "But the mayor said it was the sheriff's office, her office, then the library. Terry said that it was the mayor's, the library, then his office. And Mrs. Pear said it was the library, the sheriff's office, then the mayor's."
    r "That doesn't make any sense."

    show marley thoughtful with dissolve

    m "So... at least two people must have lied."

    show danny awkward with dissolve

    b "Or been confused. Might just be all this stress getting to them."

    show marley thoughtful with dissolve

    m "I dunno—if my place of work was attacked, I'd remember when it happened. And it's only been a few days."
    r "We have to figure out the order."

    show marley neutral with dissolve

    m "Well, in the meantime, we can at least start on the suspect files."

    show danny curious with dissolve

    b "The what?"
    m "We always make suspect files for anyone involved in the case. I'll show you what we do if you like—I can make an example one of you!"

    show danny awkward with dissolve

    b "Uh, what? You're going to make a suspect file of me? I don't think—"

    show ron neutral with dissolve

    r "Oh don't worry, it's just an example one. You're not a real suspect."
    b "Uh... well... okay. If you're sure."

    call screen d1s7_suspect_file

screen d1s7_suspect_file():
    image "black"
    text "This minigame is not available yet":
        xalign 0.5
        yalign 0.3

    imagebutton idle "images/ui/next_button.png" action Jump("d1s7_after_suspect_file"):
        xalign 0.5
        yalign 0.9

label d1s7_after_suspect_file:
    show danny neutral with dissolve

    b "Can't we do the rest later?"
    r "Sure. We also need to ask the others these questions. And we need to work out what order the attacks happened in. Why don't we go back out and see what we can find?"

    scene black with fade

    jump d1s8